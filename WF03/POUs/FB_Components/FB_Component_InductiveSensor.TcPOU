<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_Component_InductiveSensor" Id="{8e7ef9e5-be5c-4d5b-9caf-29f0d28584ab}" SpecialFunc="None">
    <Declaration><![CDATA[// function block for inductive sensor with the option of debouncing + rising and falling edge detection.
FUNCTION_BLOCK FB_Component_InductiveSensor IMPLEMENTS  I_Component
VAR_INPUT
	sName			: STRING[50];
	bRawSignal		: BOOL;				
	tDebounce		: TIME := T#0MS;
	tTimeout		: TIME := T#0MS;
END_VAR

VAR_OUTPUT
	bSensorActive	: BOOL;
	bTimeout		: BOOL;
	bRisingEdge		: BOOL;
	bFallingEdge	: BOOL;
	
	bEnabled		: BOOL;
	eState			: E_State := E_State.DISABLED;
	eNextState			: E_State := E_State.DISABLED;
	sStatusText 	: STRING[100];
END_VAR

VAR
	tDebounceTimer		: TON;
	tTimeoutTimer		: TON;
	
	bLastStableSignal	: BOOL := bRawSignal;
	bPendingSignal		: BOOL;
	
	eLastState			: E_State;
END_VAR

VAR CONSTANT
	DISABLED	: E_State := E_State.DISABLED;
	READY		: E_State := E_State.READY;
	BUSY		: E_State := E_State.BUSY;
	HALTED		: E_State := E_State.HALTED;
	TIMEOUT		: E_State := E_State.TIMEOUT;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[tDebounceTimer.PT 	:= tDebounce;
tTimeoutTimer.PT	:= tTimeout;

eState := eNextState;

CASE eState OF
	DISABLED:	sStatusText := 'Sensor disabled';
				
	READY:		sStatusText := 'Sensor idle, waiting for read';
				IF bRawSignal <> bLastStableSignal THEN
					bPendingSignal 	:= bRawSignal;
					eNextState 		:= BUSY;
				ELSE
					tTimeoutTimer(IN := FALSE);
					tDebounceTimer(IN := FALSE);
				END_IF
				
	BUSY:		sStatusText := 'Sensor is debouncing';
				tDebounceTimer(IN := TRUE);
				IF bRawSignal <> bPendingSignal THEN
					eNextState := READY;
				END_IF

				IF tDebounceTimer.Q THEN
					eNextState := TIMEOUT;
				END_IF
	TIMEOUT:	sStatusText := 'Sensor timeout';
				tTimeoutTimer(IN := TRUE);
				
				bRisingEdge  := NOT bLastStableSignal AND bPendingSignal;
				bFallingEdge := bLastStableSignal AND NOT bPendingSignal;
					
				bLastStableSignal := bPendingSignal;
				bSensorActive	  := bLastStableSignal;
				
				IF tTimeoutTimer.Q THEN
					tTimeoutTimer(IN 	:= FALSE);
					tDebounceTimer(IN 	:= FALSE);
					eNextState := READY;
				END_IF
				
	HALTED:		sStatusText := 'Sensor halted';
								
END_CASE]]></ST>
    </Implementation>
    <Method Name="M_Disable" Id="{c63d28ed-a295-4337-9a03-13b07a2f2492}">
      <Declaration><![CDATA[METHOD M_Disable : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF eState = READY THEN
	bEnabled	:= FALSE;
	eNextState	:= DISABLED;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Enable" Id="{cedd2e6b-96fc-4761-923a-ddae71ad8603}">
      <Declaration><![CDATA[METHOD M_Enable : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF eState = DISABLED THEN
	bEnabled	:= TRUE;
	eNextState	:= READY;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Halt" Id="{413c5d75-a87a-432b-97f9-5db05c61e144}">
      <Declaration><![CDATA[METHOD M_Halt : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[tDebounceTimer(IN := FALSE);
eLastState 	:= eState;
eNextState	:= HALTED;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_ResetEdge" Id="{adb0927e-0098-44f2-b9c5-8acbdc5bd39a}">
      <Declaration><![CDATA[METHOD M_ResetEdge : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[bRisingEdge		:= FALSE;
bFallingEdge	:= FALSE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Resume" Id="{f6429723-ef6d-4e50-8586-f013a7c16fec}">
      <Declaration><![CDATA[METHOD M_Resume : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF eState = HALTED THEN
	eNextState := eLastState;
END_IF]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_Component_InductiveSensor">
      <LineId Id="616" Count="0" />
      <LineId Id="614" Count="0" />
      <LineId Id="657" Count="1" />
      <LineId Id="615" Count="0" />
      <LineId Id="282" Count="0" />
      <LineId Id="566" Count="0" />
      <LineId Id="573" Count="0" />
      <LineId Id="568" Count="0" />
      <LineId Id="579" Count="5" />
      <LineId Id="574" Count="0" />
      <LineId Id="585" Count="0" />
      <LineId Id="569" Count="0" />
      <LineId Id="587" Count="6" />
      <LineId Id="586" Count="0" />
      <LineId Id="575" Count="0" />
      <LineId Id="595" Count="8" />
      <LineId Id="654" Count="1" />
      <LineId Id="604" Count="0" />
      <LineId Id="594" Count="0" />
      <LineId Id="576" Count="0" />
      <LineId Id="571" Count="0" />
      <LineId Id="577" Count="0" />
      <LineId Id="567" Count="0" />
    </LineIds>
    <LineIds Name="FB_Component_InductiveSensor.M_Disable">
      <LineId Id="5" Count="1" />
      <LineId Id="8" Count="0" />
      <LineId Id="7" Count="0" />
    </LineIds>
    <LineIds Name="FB_Component_InductiveSensor.M_Enable">
      <LineId Id="5" Count="0" />
      <LineId Id="8" Count="0" />
      <LineId Id="6" Count="1" />
    </LineIds>
    <LineIds Name="FB_Component_InductiveSensor.M_Halt">
      <LineId Id="5" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="8" Count="0" />
      <LineId Id="6" Count="0" />
    </LineIds>
    <LineIds Name="FB_Component_InductiveSensor.M_ResetEdge">
      <LineId Id="5" Count="1" />
    </LineIds>
    <LineIds Name="FB_Component_InductiveSensor.M_Resume">
      <LineId Id="7" Count="0" />
      <LineId Id="12" Count="1" />
    </LineIds>
  </POU>
</TcPlcObject>